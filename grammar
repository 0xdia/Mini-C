Function -> Type identifier ( ArgList ) CompoundStmt

ArgList -> Arg _ArgList

_ArgList -> , Arg _ArgList | ε

Arg -> Type identifier

Declaration -> Type IdentList ;

Type -> int | float

IdentList -> identifier _OrIdentList

_OrIdentList -> , IdentList | ε

Stmt -> ForStmt
| WhileStmt
| Expr ;
| IfStmt
| CompoundStmt
| Declaration
| ;

ForStmt -> for ( Expr ; OptExpr ; OptExpr ) Stmt

OptExpr -> Expr | ε

WhileStmt -> while ( Expr ) Stmt

IfStmt -> if ( Expr ) Stmt ElsePart

ElsePart -> else Stmt | ε

CompoundStmt -> { StmtList }

StmtList -> _StmtList

_StmtList -> Stmt _StmtList | ε

Expr -> identifier = Expr | Rvalue

Rvalue -> Mag _Rvalue

_Rvalue -> Compare Mag _Rvalue | ε

Compare -> == | < | > | <= | >= | !=

Mag -> Term _Mag

_Mag -> + Term _Mag | - Term _Mag | ε

Term -> Factor _Term

_Term -> * Factor _Term | / Factor _Term | ε

Factor -> ( Expr ) | - Factor | + Factor | identifier | number
