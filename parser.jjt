PARSER_BEGIN(MiniC)

public class MiniC {

    public static void main(String args[]) {
        MiniC parser = new MiniC(System.in);
        try {
            SimpleNode n = parser.Z();
            n.dump("");
            System.out.println("Thank you.");
        }
        catch (Exception e) {
            System.out.println("Oops.");
            System.out.println(e.getMessage());
            e.printStackTrace();
        }
    }
}

PARSER_END(MiniC)

/** Lexical analyzer **/

SKIP : {
    " " |
    "\t" |
    "\n" |
    "\r" |
    "\f"
}

TOKEN : {
    <IF: "if"> |
    <INT: "int"> |
    <FOR: "for"> |
    <FLOAT: "float"> |
    <ELSE: "else"> |
    <WHILE: "while"> |
    <ID: ["a"-"z", "A"-"Z", "_"](["a"-"z", "A"-"Z", "_", "0"-"9"])*> |
    <NUMBER: (["0"-"9"])*["."](["0"-"9"])+ | (["0"-"9"])+>
}

/** Root production. */

SimpleNode Z() : {}
{
    Function() <EOF>
    {
        return jjtThis;
    }
}

void Function() : {}
{
    Type() <ID> "(" ArgList() ")" CompoundStmt()
}

void ArgList() : {}
{
    Arg() _ArgList()
}

void _ArgList() : {}
{
    "," Arg() _ArgList() | {}
}

void Arg() : {}
{
    Type() <ID>
}

void Declaration() : {}
{
    Type() IdentList() ";"
}

void Type() : {}
{
    <INT> | <FLOAT>
}

void IdentList() : {}
{
    <ID> _OrIdentList()
}

void _OrIdentList() : {}
{
    "," IdentList() | {}
}

void Stmt() : {}
{
    ForStmt() |
    WhileStmt() |
    Expr() ";" |
    IfStmt() |
    CompoundStmt() |
    Declaration() |
    ";"
}

void ForStmt() : {
    int label = 0;
}
{
    <FOR> "(" Expr() ";" OptExpr() ";" OptExpr() ")" Stmt()
    {
        label ++;
        System.out.println(label);
    }
}

void OptExpr() : {}
{
    Expr() | {}
}

void WhileStmt() : {}
{
    <WHILE> "(" Expr() ")" Stmt()
}

void IfStmt() : {}
{
    <IF> "(" Expr() ")" Stmt() ElsePart()
}

void ElsePart() : {}
{
    <ELSE> Stmt() | {}
}

void CompoundStmt() : {}
{
    "{" StmtList() "}"
}

void StmtList() : {}
{
    _StmtList()
}

void _StmtList() : {}
{
    Stmt() _StmtList() | {}
}

void Expr() : {}
{
    LOOKAHEAD(2) <ID> "=" Expr() | Rvalue()
}

void Rvalue() : {}
{
    Mag() _Rvalue()
}

void _Rvalue() : {}
{
    Compare() Mag() _Rvalue() | {}
}

void Compare() : {}
{
    "==" | "<" | ">" | "<=" | ">=" | "!="
}

void Mag() : {}
{
    Term() _Mag()
}

void _Mag() : {}
{
    "+" Term() _Mag() | "-" Term() _Mag() | {}
}

void Term() : {}
{
    Factor() _Term()
}

void _Term() : {}
{
    "*" Factor() _Term() |
    "/" Factor() _Term() |
    {}
}

void Factor() : {}
{
    "(" Expr() ")" |
    "+" Expr() |
    "-" Expr() |
    <ID> |
    <NUMBER>
}
